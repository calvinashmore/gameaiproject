FUNCTION_BLOCK basic_enjoyment_test

VAR_INPUT
        euphoria : REAL;
        irritation : REAL;
        anxiety : REAL;
        depressant : REAL;
        stimulant : REAL;

        outlook : REAL;
END_VAR

VAR_OUTPUT
        response : REAL;
        d_euphoria : REAL;
        d_irritation : REAL;
END_VAR

FUZZIFY euphoria
	TERM normal := (0, 1) (20, 1) (40, 0) ;
	TERM happy := (20, 0) (40,1) (60,1) (80,0);
	TERM ecstatic := (60, 0) (80, 1);
END_FUZZIFY

FUZZIFY irritation
	TERM normal := (0, 1) (20, 1) (40, 0) ;
	TERM annoyed := (20, 0) (40,1) (60,1) (80,0);
	TERM angry := (60, 0) (80, 1);
END_FUZZIFY

FUZZIFY anxiety
	TERM low := (0, 1) (20, 1) (40, 0) ;
	TERM medium := (20, 0) (40,1) (60,1) (80,0);
	TERM high := (60, 0) (80, 1);
END_FUZZIFY

FUZZIFY stimulant
        TERM none := (0, 1) (9, 1) (10, 0);
	TERM low := (9, 0) (10, 1) (20, 1) (40, 0) ;
	TERM medium := (20, 0) (40,1) (60,1) (80,0);
	TERM high := (60, 0) (80, 1);
END_FUZZIFY

FUZZIFY depressant
        TERM none := (0, 1) (9, 1) (10, 0);
	TERM low := (9, 0) (10, 1) (20, 1) (40, 0) ;
	TERM medium := (20, 0) (40,1) (60,1) (80,0);
	TERM high := (60, 0) (80, 1);
END_FUZZIFY

FUZZIFY outlook
        TERM very_negative := (-100,1) (-50,0);
        TERM negative := (-100,0) (-50,1) (0,0);
        TERM neutral := (-50,0) (0,1) (50,0);
        TERM positive := (0,0) (50, 1) (100,0);
        TERM very_positive := (50,0) (100,1);
END_FUZZIFY

DEFUZZIFY response
        TERM very_negative := (-10,1) (-5,0);
        TERM negative := (-10,0) (-5,1) (0,0);
        TERM neutral := (-5,0) (0,1) (5,0);
        TERM positive := (0,0) (5, 1) (10,0);
        TERM very_positive := (5,0) (10,1);
	METHOD : COG;
	DEFAULT := 0;
END_DEFUZZIFY

DEFUZZIFY d_euphoria
        TERM very_negative := (-10,1) (-5,0);
        TERM negative := (-10,0) (-5,1) (0,0);
        TERM neutral := (-5,0) (0,1) (5,0);
        TERM positive := (0,0) (5, 1) (10,0);
        TERM very_positive := (5,0) (10,1);
	METHOD : COG;
	DEFAULT := 0;
END_DEFUZZIFY

DEFUZZIFY d_irritation
        TERM very_negative := (-10,1) (-5,0);
        TERM negative := (-10,0) (-5,1) (0,0);
        TERM neutral := (-5,0) (0,1) (5,0);
        TERM positive := (0,0) (5, 1) (10,0);
        TERM very_positive := (5,0) (10,1);
	METHOD : COG;
	DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK No2
	AND : MIN;			// Use 'min' for 'and' (also implicit use 'max' for 'or' to fulfill DeMorgan's Law)
	ACT : MIN;			// Use 'min' activation method
	ACCU : MAX;			// Use 'max' accumulation method

        // Combs method

        RULE 1 : IF euphoria IS ecstatic    THEN    response IS very_positive;
        RULE 2 : IF euphoria IS happy       THEN    response IS positive;
        RULE 3 : IF euphoria IS normal      THEN    response IS positive;

        RULE 4 : IF irritation IS angry     THEN    response IS very_negative;
        RULE 5 : IF irritation IS annoyed   THEN    response IS negative;

        RULE 7 : IF anxiety IS high OR anxiety IS medium     THEN    response IS negative;

        // effect of stimulants and depressants

        RULE 8 : IF depressant IS medium THEN response IS very_positive; // fun drunk level
        RULE 9 : IF depressant IS high THEN response IS neutral; // too drunk to care level

        RULE 10 : IF stimulant IS medium THEN response IS positive; // fun drunk level
        RULE 11 : IF stimulant IS high THEN response IS positive; // pissed off drunk level

        // CHANGES TO INTERNAL STATE

        RULE 21 : IF euphoria IS ecstatic    THEN    d_euphoria IS positive; // so happy, talking about it makes you
        RULE 22 : IF euphoria IS happy       THEN    d_euphoria IS positive; // even happier!
        RULE 23 : IF euphoria IS normal      THEN    d_euphoria IS neutral;

        RULE 24 : IF irritation IS angry     THEN    d_irritation IS very_positive;
        RULE 25 : IF irritation IS annoyed   THEN    d_irritation IS positive;

        RULE 26 : IF outlook IS very_positive THEN response IS very_positive, d_euphoria IS very_positive;
        RULE 26 : IF outlook IS positive THEN response IS positive, d_euphoria IS positive;
        RULE 26 : IF outlook IS negative THEN response IS negative, d_euphoria IS negative;
        RULE 26 : IF outlook IS very_negative THEN response IS very_negative, d_euphoria IS very_negative;

END_RULEBLOCK

END_FUNCTION_BLOCK
